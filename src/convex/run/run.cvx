;;
;;
;; Special transactions supported by the runner.
;;
;; Defined as function but they throw a fatal error when used as regular Convex Lisp code.
;; Also act as documentation holders.
;;
;;
(do

  (def strx
       (deploy '(do

                  (call *registry*
                        (cns-update 'convex.run.strx 
                                    *address*))


                  ;; SPECIAL VALUES

                  (def *cycle*

                    ^{:doc {:description ["Starting at 0, is incremented every time the main file is run."
                                          "Potentially useful for watch mode."]}}

                    nil)



                  (def *error*

                    ^{:doc {:description ["Any thrown exception is defined under this symbol."
                                          "Notably useful in try-catch (see `try`)"]}}

                    nil)



                  (def *file*

                    ^{:doc {:description "Canonical path of the invoked main file."}}

                    nil)



                  (def *juice.last*

                    ^{:doc {:description "The amount of juice consumed by the previous transaction."}}

                    nil)



                  (def *juice.total*

                    ^{:doc {:description "Total amount of juice consumed up to now."}}

                    nil)



                  (def *trx.form*

                    ^{:doc {:description "Form representing the current transaction."}}

                    nil)



                  (def *trx.id*

                    ^{:doc {:description ["Starting at 0, is incremented on every transaction."
                                          "Resets on each run in watch mode."]}}

                    nil)

                
                  ;; SPECIAL TRANSACTIONS
                
                
                  (defmacro dep
                
                    ^{:doc {:description ["Special transaction which loads Convex Lisp dependencies files. During watch mode, those files are live-reloaded as well."
                                          "If needed, MUST be written as the very first transaction found in the main file."
                                          "Takes a map of `symbol` -> `path to file`. Files are read as vectors of Convex Lisp forms and then defined under their corresponding symbol."
                                          "Then, those forms can be used as needed. It is common to use `deploy` or `eval`, depending on the purpose of a given file."]
                            :examples    [{:code "(strx/dep {my-lib \"path/to/lib.cvx\"})"}]
                            :signature   [{:params [sym->file]}]
                            :type        :macro}}
                
                    [sym->file]
                
                    (assert (not "First transaction")))
                
                
                
                  (defmacro do
                
                    ^{:doc {:description ["Executes each argument as a transaction."
                                          "Useful when composing special transactions in some way"]
                            :examples    [{:code "(strx/do (def foo 42) (strx/out foo))"}]
                            :signature   [{:params [& trx+]}]
                            :type        :macro}}
                
                    [& trx+]

                    [:cvm.strx :do `(quote ~trx+)])
                


                  (defn do-splice
                
                    ^{:doc {:description ["Evaluates the given argument, result must be a vector of transactions."
                                          "Executes those transactions one by one."
                                          "Similar to `do` but more dynamic."]
                            :examples    [{:code "(strx/do (def x (quote [(def foo 42) (strx/out foo)])) (strx/splice x))"}]
                            :signature   [{:params [trx+]}]
                            :type        :macro}}
                
                    [trx+]

                    [:cvm.strx :do trx+])
                


                  (defmacro env
                
                    ^{:doc {:description ["Retrieves environment variable from the current process. Useful when some information must be passed to the runner from the outside."
                                          "When a symbol is given, defines under that symbol the entire map of `env property (string)` -> `env value (string)`."
                                          "When a env variable is given as well, defines only the value for that variable"]
                            :examples    [{:code "(strx/env env-map"}
                                          {:code "(strx/env my-x-value \"MY_X\")"}]
                            :signature   [{:params [sym]}
                                          {:params [sym env-var]}]
                            :type        :macro}}
                
                
                    ([sym]

                     `(*address*/env ~sym
                                     nil))
                
                
                    ([sym env-var]

                     `(let [env-var ~env-var
                            sym     ~sym]
                        (assert (symbol? sym))
                        (assert (or (str? env-var)
                                    (nil? env-var)))
                        [:cvm.strx :env sym env-var])))
                
                
                
                  (defmacro hook.end
                
                    ^{:doc {:description ["Registers the given transactions to be executed at the end, even if an error occured."
                                          "Overwrites what was previously registers."
                                          "Providing no transaction cancels what was previously registered."]
                            :examples    [{:code "(strx/hook.end)"}
                                          {:code "(strx/hook.end (def foo 42) (strx/out [:end :foo foo]))}"}]
                            :signature   [{:params [& trx+]}]
                            :type        :macro}}
                
                    [& trx+]
                
                    [:cvm.strx :hook.end `(quote ~trx+)])
                
                
                
                  (defmacro hook.error
                
                    ^{:doc {:description ["When an exception occurs, the given function is called with that exception. The result is code that is interpreted."
                                          "Returned code can be a special transaction."
                                          "Default is outputting the error without any modification"
                                          "Overwrites any previously registered function, `nil` removes it."]
                            :examples    [{:code "(strx/hook.error (fn [ex] `(strx/out [:EXCEPTION ~ex])))"}]
                            :signature   [{:params [f]}]
                            :type        :macro}}
                
                    [f]
                
                    `(let [f ~f]
                       (assert (fn? f))
                       [:cvm.strx :hook.error f]))
                
                
                
                  (defmacro hook.out
                
                    ^{:doc {:description ["Registers a function that modifies any value prior to being outputted with `out`"
                                          "Default is `identity`"
                                          "If the function returns nil, then nothing is outputted."
                                          "Overwrites any previously registered function, `nil` removes it."]
                            :examples    [{:code "(strx/hook.out (fn [x] (when-not (:foo x) x)))"}]
                            :signature   [{:params [f]}]
                            :type        :macro}}
                
                    [f]

                    `(let [f ~f]
                       (assert (fn? f))
                       [:cvm.strx :hook.out f]))
                
                
                
                  (defmacro hook.trx
                
                    ^{:doc {:description ["Registers a function that modifies a regular transaction (not any special one)."
                                          "Useful for decorating all subsequent transactions or filtering them based on metadata."
                                          "Overwrites any previously registered function, `nil` removes it."]
                            :examples    [{:code "(strx/hook.trx (fn [trx] `(strx/out [(quote ~trx) ~trx])))"}]
                            :signature   [{:params [f]}]
                            :type        :macro}}
                
                    [f]

                    `(let [f ~f]
                       (assert (fn? f))
                       [:cvm.strx :hook.trx f]))
                
                                                                        
                
                  (defmacro log
                
                    ^{:doc {:description "Defines the current CVM log under the given symbol"
                            :examples    [{:code "(strx/do (log :foo) (strx/log l) (strx/out l))"}]
                            :signature   [{:params [sym]}]
                            :type        :macro}}
                
                    [sym]
                
                    `(let [sym ~sym]
                       (assert (symbol? sym))
                       [:cvm.strx :log sym]))
                
                
                
                  (defmacro out
                
                    ^{:doc {:description "Special transaction which outputs the given argument."
                            :examples    [{:code "(strx/out 42)"}]
                            :signature   [{:params [x]}]
                            :type        :macro}}
                
                    [x]

                    [:cvm.strx :out x])
                
                
                
                  (defmacro read
                
                    ^{:doc {:description ["Passes the given string through the Convex Lisp reader and returns a vector of forms."
                                          "Those forms can be compiled, evaluated, ..."]
                            :examples    [{:code "(strx/read \"(+ 2 2)\")"}]
                            :signature   [{:params [string]}]
                            :type        :macro}}
                
                    [src]
                
                    `(let [src ~src]
                       (assert (str? src))
                       [:cvm.strx :read src]))
                

                
                  (defmacro screen.clear
                
                    ^{:doc {:description ["Clears the screen."
                                          "Useful only if the output is printed in the terminal."]
                            :examples    [{:code "(strx/clear.screen)"}]
                            :signature   [{:params []}]
                            :type        :macro}}
                
                    []

                    [:cvm.strx :screen.clear])
                
                
                
                  (defmacro try
                
                    ^{:doc {:description ["Given transactions are executed one by one until an exception is thrown."
                                          "In such case, the exception is defined under `*cvm.error*` and transactions in the catch form are executed (see `catch`)."
                                          "With or without a catch form, execution outside of `try` continues."]
                            :examples    [{:code "(strx/try (def foo 42) (fail \"Oops\") (strx/catch (strx/out [:error *cvm.error*])))"}]
                            :signature   [{:params [& trx+]}]
                            :type        :macro}}
                                        
                
                    ([try-trx+]

                     `(*address*/try try-trx+
                                     nil))


                    ([try-trx+ catch-trx+]

                     (assert (vector? try-trx+))
                     (assert (or (nil? catch-trx+)
                                 (vector? catch-trx+)))
                     [:cvm.try :try try-trx+ catch-trx+]))
                                
                
                  ))) ;; END - STRX



  (def help
       (deploy '(do

                  (call *registry*
                        (cns-update 'convex.run.help
                                    *address*))



                  (defn line+

                    [section+]

                    (apply str
                           (reduce (fn [acc [section line+]]
                                     (reduce (fn [acc-2 line]
                                               (conj acc-2
                                                     \newline
                                                     \newline
                                                     line))
                                             (conj acc
                                                   \newline
                                                   \newline
                                                   \newline
                                                   section
                                                   \newline
                                                   (loop [acc []
                                                          i   (count section)]
                                                    (if (> i
                                                           0)
                                                      (recur (conj acc
                                                                   "=")
                                                             (dec i))
                                                      (apply str
                                                             acc))))
                                             line+))
                                   []
                                   section+)))



                  (let [-doc (fn [target meta]
                               (let [doc         (:doc meta)
                                     description (:description doc)
                                     example+    (:examples doc)
                                     signature   (:signature doc)
                                     type        (:type doc)
                                     target-sym  (symbol target)
                                     section+    [["HELP"
                                                   [target]]]
                                     section-2+  (if type
                                                   (conj section+
                                                         ["TYPE"
                                                          [type]])
                                                   section+)
                                     section-3+  (if signature
                                                   (conj section-2+
                                                         ["SIGNATURE"
                                                          (map (fn [sign]
                                                                 (cons target-sym
                                                                       (:params sign)))
                                                               signature)])
                                                   section-2+)
                                     section-4+  (if description
                                                   (conj section-3+
                                                         ["DESCRIPTION"
                                                          (cond
                                                            (str? description)    [description]
                                                            (vector? description) description
                                                            :else                 ["Malformed, description must be a string or a vector or strings."])])
                                                   section-3+)
                                     section-5+  (if example+
                                                   (conj section-4+
                                                         ["EXAMPLES"
                                                          (map :code
                                                               example+)])
                                                   section-4+)]
                                 (line+ section-5+)))]

                      (defn doc


                        ([addr-or-sym]

                         (if (symbol? addr-or-sym)
                           (-doc (str addr-or-sym)
                                 (lookup-meta addr-or-sym))
                           (if-let [acc (account addr-or-sym)]
                             (let [allowance   (acc :allowance)
                                   balance     (acc :balance)
                                   controller  (acc :controller)
                                   env         (acc :environment)
                                   key         (acc :key)
                                   meta        (acc :metadata)
                                   section+    [["ACCOUNT"
                                                 [addr-or-sym]]
                                                ["KEY"
                                                 [(or key
                                                      "None (this is an actor)")]]
                                                ["ALLOWANCE"
                                                 [allowance]]
                                                ["BALANCE"
                                                 [balance]]
                                                ["CONTROLLER"
                                                 [(or controller
                                                      "None")]]]]
                               (line+ (if (> (count env)
                                             0)
                                        (conj section+
                                              ["ENVIRONMENT"
                                               (reduce (fn [line+ [k _v]]
                                                         (if-let [desc (get-in meta
                                                                               [k
                                                                                :doc
                                                                                :description])]
                                                           (conj line+
                                                                 k
                                                                 (str "  "
                                                                      (if (str? desc)
                                                                        desc
                                                                        (first desc))))
                                                           (conj line+
                                                                 k)))
                                                       []
                                                       env)])
                                        section+)))
                             (str "Account "
                                  addr-of-sym
                                  " does not exist."))))


                        ([addr sym]

                         (-doc (str (address addr)
                                             "/"
                                             sym)
                               (lookup-meta addr
                                            sym)))))


                    )))  ;; End - HELP



  ;; CVM result, needed for easily getting the address of the strx library.
  ;;
  strx


  ) ;; END 
